// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    String urlStr = "http://mvn.mob.com/android"
    if (rootProject.hasProperty("mavenUrl")) {
        urlStr = rootProject.getProperty("mavenUrl")
    }
    println "mavenUrl: ${urlStr}, ${rootProject.hasProperty("mavenUrl")}"
    ext {
        mavenUrl = urlStr
    }

    repositories {
        jcenter()
        mavenCentral()
        maven {
            url = mavenUrl
        }
        google()
        //====================== snapchat ======================
        maven {
            url "https://storage.googleapis.com/snap-kit-build/maven"
        }
        //====================== snapchat ======================
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath 'com.mob.sdk:MobSDK:+'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url = mavenUrl
        }
        google()
        //====================== snapchat ======================
        maven {
            url "https://storage.googleapis.com/snap-kit-build/maven"
        }
        //====================== snapchat ======================
    }
    configurations.all {
        resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

String findBuildTools() {
    def localProperties = new File(project.rootDir, "local.properties")
    if (localProperties.exists()) {
        Properties properties = new Properties()
        localProperties.withInputStream {
            instr->properties.load(instr)
        }
        def sdkDir = properties.getProperty('sdk.dir')
        def buildTools = new File(sdkDir, "build-tools")
        if (buildTools.exists()) {
            def tools = buildTools.list()
            if (tools != null) {
                Arrays.sort(tools)
                return tools[tools.length - 1]
            }
        }
    }
    return null
}

